import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation


plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.google.protobuf" version "0.8.17"
    id "idea"
    id 'java'
    id "maven-publish"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group 'com.github.technove'
version '3.2-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }

    generateProtoTasks.generatedFilesBaseDir = 'generated-sources'
    generatedFilesBaseDir = "$projectDir/src/generated"
}

shadowJar {
    minimize()
    includeEmptyDirs false
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "co.technove.flare.libs"

}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

jar {
    enabled = false
    dependsOn(shadowJar { classifier = null })
}

dependencies {
    compileOnly 'org.jetbrains:annotations:21.0.1'
    implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'

    implementation 'com.google.protobuf:protobuf-javalite:3.17.3'
    implementation 'com.google.protobuf:protobuf-java-util:3.14.0'

}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}
